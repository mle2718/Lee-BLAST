<<dd_version: 2>>


<!---A small document to read in and display some model results

THIS IS A PLACEHOLDER ---> 

<!---Execute this with:



if strmatch("$user","minyangWin"){
	global project_dir  "C:/Users/Min-Yang.Lee/Documents/BLAST/cod_haddock_fy2024" 
	global MRIP_root  "V:/READ-SSB-Lee-MRIP-BLAST/"
}



if strmatch("$user","minyangNix"){
	global project_dir "${myroot}/BLAST/READ-SSB-Lee-BLAST/cod_haddock_fy2024"
	global MRIP_root "${myroot}/BLAST/READ-SSB-Lee-MRIP-BLAST/" 
}

global mrip_vintage "2023_11_07"

/* setup directories */
global code_dir "${project_dir}/code"
global source_data "${project_dir}/source_data"
global working_data "${project_dir}/working_data"
global output_dir "${project_dir}/output"
global MRIP_dir  "${MRIP_root}/data_folder/main/MRIP_${mrip_vintage}"
global MRIP_raw  "${MRIP_root}/data_folder/raw"

dyndoc "${code_dir}/postsim/status_quo_summaries.txt", saving(${project_dir}/status_quo_summaries.html) replace

---> 
~~~~
<<dd_do:quietly>>
/****************************/
/****************************/
/* Haddock and Cod rec ACLs in mt*/
/****************************/
/****************************/
global haddock_mort2022=666


global cod_recACL2022=192
global hadd_recACL2022=3634

global cod_recACL2023=192
global hadd_recACL2023=610

global cod_calibration_adj=25
global hadd_calibration_adj=-24.5

global adj_cod_recACL2023=$cod_recACL2023-$cod_calibration_adj
global adj_hadd_recACL2023=$hadd_recACL2023-$hadd_calibration_adj

global mrip_vintage "2023_11_02"

<</dd_do>>
~~~~

# Inputs and Background

Previous years Regulations
==============================================================

2022 Regulations
==============================================================
The recreational sub-ACL for GoM cod is <<dd_display: %8.0fc $cod_recACL2022>> mt for FY 2022. The recreational sub-ACL for GOM haddock is <<dd_display: %8.0fc $hadd_recACL2022>> mt.

Haddock:

*    20 fish, 17"
*    Open May1-Feb 28 and April 1- April 30
*    The change from 2021 is to increase the possession limit from 15 to 20 haddock

Cod: 

*   1 fish, 22"
*   Open Sept 1-October 7 and April 1-April 14 for private anglers
*   Open Sept 1-October 7 and April 1-April 14 for the for-hire anglers



Fishing Year 2023 Regulations
==============================================================
The recreational sub-ACL for GoM cod is <<dd_display: %8.0fc $cod_recACL2023>> mt for FY 2023. The recreational sub-ACL for GOM haddock is <<dd_display: %8.0fc $hadd_recACL2023>> mt.  Regulations were



Haddock:

*    For-Hire sector: 15 fish with an 18" minimum size.
*    Private Anglers: 10 fish with a 17" minimum size.
*    Open May1-Feb 28 and April 1- April 30. Closed for the month of March.

Cod: 

*   1 fish, 22"
*   Open Sept 1-October 31 for all anglers.





Input data
==============================================================
1. MRIP data extracted and procesed on <<dd_display: "$mrip_vintage">>. 
1.  **Monthly** length-frequencies for cod and haddock
1.  **Monthly** catch-per-trip for cod and haddock
1.  2023 numbers at age for cod and haddock from the AGEPRO projections.
1.  Bottomtrawl survey data from 2018-2023 to form the age-length keys.
1.  Updated economic model parameters from Carr-Harris and Steinback (forthcoming in JOCE).

Calibration 
===============================================================

1. I'm using the number of choice occasions to calibrate the model so the simulated number of trips matches the actual.  
1. I'm using the "always on" behavior -- if an angler hits their bag limit for a species, they continue to fish but will discard any additional fish of that species, regardless of size.  For months when cod is closed, we set the possession limit to 5 this allows a very small number of trips to take a few cod. For months when haddock is closed, we set the possession limit to 15 fish in FY2021 and 20 fish in 2022. This does the same thing. 
1. The partial months are difficult to model.  For April, we run one model where april is open for cod and one where it is closed and then average the two.  October is open for 1 week.  I have been running one model where October is open for cod and one where it is closed and then taking a weighted average (1 part open and 3 parts closed) of the two.

# Simulations 

<!---
1. I'm using the number of choice occasions to calibrate the model so the simulated number of trips matches the actual.  
1. I'm using the "always on" behavior -- if an angler hits their bag limit for a species, they continue to fish but will discard any additional fish of that species, regardless of size.  For months when cod is closed, we set the possession limit to 5 and the minimum size to 100" -- this allows a very small number of trips to take a few cod. For months when haddock is closed, we set the possession limit to 15 and the minimum size to 100". This does the same thing. 
1.  I'm ignoring the Oct1 to Oct 7 cod opening for the for-hire fleet. But I'm also ignoring the Sept 1 to Sept 7 cod closure for the for hire fleet. So, we should look at Sept and October together.
SQ averages scenario numbers 1 and 3.   Scenario 1 is "April open for haddock, open for cod" and Scenario 3 is "April open for haddock, closed for cod."
Whole month of April for Cod and closed for haddock (scenario 4).
Whole month of April for both Cod and Haddock scenario 1.
--->


##  Scenario Key and Notes


| Scenario number| Short Description | Long Description| Status|
| :-----        |    :---------------   |          :--------------------- |   :-----|
| 0| Status Quo 2023       | Same regs from 2022.   | Run|
| 1| April Oct Closed		| Same regs from 2022, except April and October are closed for cod| Run|
| 2| April Oct Open		| Same regs from 2022, except April and October are open for cod| Run|
| 3| Haddock15A			| Same regs as 1, except with a 15 haddock cod limit | Run|
| 4| Haddock15B			| Same regs as 2, except with a  15 haddock cod limit | Run|
| 5| Haddock15C			| Same regs as 2022, except with a  15 haddock cod limit | Run|
| 6| Haddock12A			| Same regs as 1, except with a 12 haddock cod limit | Run|
| 7| Haddock12B			| Same regs as 2, except with a  12 haddock cod limit | Run|
| 8| Haddock12C			| Same regs as 2022, except with a  12 haddock cod limit | Run|
| 9| Haddock10A			| Same regs as 1, except with a 10 haddock cod limit | Run|
| 10| Haddock10B			| Same regs as 2, except with a  10 haddock cod limit | Run|
| 11| Haddock10C			| Same regs as 2022, except with a  10 haddock cod limit | Run|
| 101| Scott2 		| Similar to 11, except Sept+October open; April closed | Run|
| 111| Scott1 		| Same regs as 101, except Sept+October open; April 1-15 | Run|
| 18| RAP18		| Cod: Sept+Oct. Haddock: May1-Feb 28, April1-30.  18"  | Run|
| 19| RAP19		| Cod: Sept+Oct. Haddock: May1-Feb 28, April1-30.  19"  | Run|
| 20| RAP20		| Cod: Sept+Oct. Haddock: May1-Feb 28, April1-30.  20"  | Run|
| 217| CMTE2_17		| Cod: Sept+Oct. Haddock: April1-Feb 28.  17"  | Run|
| 218| CMTE2_18		| Cod: Sept+Oct. Haddock: April1-Feb 28.  18"  | Run|

<!---
| Scenario number| Short Description | Long Description| Status|
| :-----        |    :---------------   |          :--------------------- |   :-----|
| 0| Status Quo 2022		| Same regs from 2022.   | Setup|
| 1| April Oct Closed		| Same regs from 2022, except April and October are closed for cod| Setup|
| 2| April Oct Open		| Same regs from 2022, except April and October are open for cod| Setup|
| 3| April Oct Closed		| Same regs as 1, except with a 21" cod limit | Setup|
| 4| April Oct Open		| Same regs as 2, except with a 21" cod limit | Setup|
| 5| Status quo with 21" cod		| Same regs as SQ, except with a 21" cod limit | Setup|

101 , I will take the weighted average of 9 and 10.
	1. I will set the weights for scenario_num==10 & month==4 to zero. Sets the april outcomes to the outcomes from Scenario 9 (April = closed). I will set the weights for
			Scenario_num=9 and month=10 to zero. This sets the october outcomes to the outcomes from scenario 10 (october open).
			
	2.  To get 111 I will take a weighted average of 9 and 10.
				Scenario_num=9 and month=10 to zero. This sets the october outcomes to the outcomes from scenario 10 (october open).
				equal weight the april month for both senarios 9 and 10.


 ---> 




# Annual Results 

Here are some results.  In this section, I first compute the annual totals for every replicate. Then I report out on the medians.


<<dd_do:quietly>>


/****************************/
/****************************/
/* FOLDER setups */
/****************************/
/****************************/

/*minyangWin is setup to connect to oracle yet */
if strmatch("$user","minyangWin"){
	global project_dir  "C:/Users/Min-Yang.Lee/Documents/BLAST/cod_haddock_fy2023" 
	global MRIP_root  "V:/READ-SSB-Lee-MRIP-BLAST/"
	global 	oracle_cxn  " $mysole_conn"
}

if strmatch("$user","minyangNix"){
	global project_dir "${myroot}/BLAST/READ-SSB-Lee-BLAST/cod_haddock_fy2023"
	global MRIP_root "${myroot}/BLAST/READ-SSB-Lee-MRIP-BLAST/" 
}
/* setup directories */
global code_dir "${project_dir}/code"
global source_data "${project_dir}/source_data"
global working_data "${project_dir}/working_data"
global output_dir "${project_dir}/output"
global MRIP_dir  "${MRIP_root}/data_folder/main/MRIP_${mrip_vintage}"
global MRIP_raw  "${MRIP_root}/data_folder/raw"



local my_common1 "atlanticcod"
local my_common2 "haddock"


global cm_to_inch=0.39370787
global cod_naa "${source_data}/cod agepro/historical_and_mean_projected_Cod_NAA.dta"
global cod_naa_start "${source_data}/cod agepro/NAA_GOM_COD_2021_UPDATE_BOTH.dta"

global codalkey "${working_data}/cod_al_key.dta"



global working_year 2023
global previous=$working_year-1


global output_dir "${project_dir}/output"

/****************************/
/****************************/
/* Files to read in  */
/****************************/
/****************************/




/* Read in all the Econ model runs that match `stub' */
local estub "economic_data_2023_SQ_"
local estub2 "economic_data_2023_haddock"
local estub3 "economic_data_2023_RAP_"
local estub4 "economic_data_2023_CMTE_"

local efilelist1: dir "${output_dir}" files "`estub'*.dta"
local efilelist2: dir "${output_dir}" files "`estub2'*.dta"
local efilelist3: dir "${output_dir}" files "`estub3'*.dta"
local efilelist4: dir "${output_dir}" files "`estub4'*.dta"


local ecombinedfiles `" `efilelist1' `efilelist2'  `efilelist3' `efilelist4' "'




/****************************/
/****************************/
/* Read in results and parse the source  */
/****************************/
/****************************/
clear
gen str40 source=""
foreach file of local ecombinedfiles{
capture append using ${output_dir}/`file'
replace source="`file'"  if source==""
}
keep scenario scenario_num month replicate WTP


tempfile econ_data
save `econ_data', replace









/* Read in all the model runs that match `stub' */
local stub "recreational_catches_2023_SQ_"
local stub2 "recreational_catches_2023_haddock"
local stub3 "recreational_catches_2023_RAP_"
local stub4 "recreational_catches_2023_CMTE_"

local filelist1: dir "${output_dir}" files "`stub'*.dta"
local filelist2: dir "${output_dir}" files "`stub2'*.dta"
local filelist3: dir "${output_dir}" files "`stub3'*.dta"
local filelist4: dir "${output_dir}" files "`stub4'*.dta"

*local combinedfiles `" `filelist1' `filelist2' `filelist3' `filelist4' `filelist5' "'

local combinedfiles `" `filelist1' `filelist2'  `filelist3' `filelist4' "'


/****************************/
/****************************/
/* Read in results and parse the source  */
/****************************/
/****************************/
clear
gen str40 source=""
foreach file of local combinedfiles{
capture append using ${output_dir}/`file'
replace source="`file'"  if source==""
}

merge 1:1 scenario scenario_num month replicate using `econ_data'

split source, parse("/") gen(ss)
scalar rnvars=r(nvars)
local all=r(varlist)
local m="ss"+scalar(rnvars)
local dropper : list all - m


/****************************/
/****************************/
/* Tidy ups
Drop the rows corresponding to the previous fishing year. 
Generate the fishing year variable
construct total catch (numbers) and mortality (mt)
 */
/****************************/
/****************************/



drop if month<=4
gen fishing_year=$working_year
gen year=fishing_year
gen month_of_fy=month-4

replace year=year+1 if month>=13
replace month=month-12 if month>=13



/* construct mean outcomes of as-run scenarios*/
collapse (mean) choice_occasions total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_kept_mt cod_released_mt cod_released_dead_mt hadd_kept_mt hadd_released_mt hadd_released_dead_mt crep hrep codbag_comply cod_sublegal_keep cod_release_mort hadd_release_mort WTP ,by(scenario_num fishing_year year month replicate month_of_fy source)

tempfile s_as_run
save `s_as_run', replace emptyok










use `s_as_run', clear


/* construct the  SQ scenario by taking a weighted average of the corresponding 1 and 2 scenarios.
Scenario 5 is a weighted average of 3 and 4
Scenario 8 is a weighted average of 6 and 7*/

*keep if inlist(scenario_num,1,2,3,4,6,7) 
gen freq=1
replace freq=3 if month==10 & inlist(scenario_num,1,3,6,9)
replace scenario_num=0 if inlist(scenario_num,1,2)
replace scenario_num=5 if inlist(scenario_num,3,4)
replace scenario_num=8 if inlist(scenario_num,6,7)
replace scenario_num=11 if inlist(scenario_num,9,10)
keep if inlist(scenario_num,0,5,8,11)


collapse (mean) choice_occasions total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_kept_mt cod_released_mt cod_released_dead_mt hadd_kept_mt hadd_released_mt hadd_released_dead_mt crep hrep codbag_comply cod_sublegal_keep cod_release_mort hadd_release_mort WTP [fweight=freq] ,by(scenario_num fishing_year year month replicate month_of_fy source)


tempfile smodified
save `smodified', replace emptyok



/* work on scenario 101 


To get 101, I will take the weighted average of 9 and 10.
	1. I will set the weights for scenario_num==10 & month==4 to zero. Sets the april outcomes to the outcomes from Scenario 9 (April = closed). I will set the weights for
			Scenario_num=9 and month=10 to zero. This sets the october outcomes to the outcomes from scenario 10 (october open).
	*/		




use `s_as_run', clear


keep if inlist(scenario_num,9,10) 
gen freq=1
replace freq=0 if month==10 & inlist(scenario_num,9)
replace freq=0 if month==4 & inlist(scenario_num,10)

replace scenario_num=101
collapse (mean) choice_occasions total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_kept_mt cod_released_mt cod_released_dead_mt hadd_kept_mt hadd_released_mt hadd_released_dead_mt crep hrep codbag_comply cod_sublegal_keep cod_release_mort hadd_release_mort WTP [fweight=freq] ,by(scenario_num fishing_year year month replicate month_of_fy source)
append using `smodified'
save `smodified', replace emptyok




/* work on scenario 111 
	2.  To get 111 I will take a weighted average of 9 and 10.
				Scenario_num=9 and month=10 to zero. This sets the october outcomes to the outcomes from scenario 10 (october open).
				equal weight the april month for both senarios 9 and 10.
*/		


use `s_as_run', clear

keep if inlist(scenario_num,9,10) 
gen freq=1
replace freq=0 if month==10 & inlist(scenario_num,9)

replace scenario_num=111
collapse (mean) choice_occasions total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_kept_mt cod_released_mt cod_released_dead_mt hadd_kept_mt hadd_released_mt hadd_released_dead_mt crep hrep codbag_comply cod_sublegal_keep cod_release_mort hadd_release_mort WTP [fweight=freq] ,by(scenario_num fishing_year year month replicate month_of_fy source)

append using `smodified'
save `smodified', replace emptyok


append using `s_as_run'











gen cod_tot_cat=cod_num_kept+cod_num_released
gen hadd_tot_cat=haddock_num_kept+haddock_num_released

sort month
order cod_tot_cat hadd_tot_cat, after(month)
format *num* %09.1gc
format *mt %06.1fc
format *tot_cat %09.1gc

gen cod_mort_mt=cod_kept_mt+cod_released_dead_mt
gen hadd_mort_mt=hadd_kept_mt+hadd_released_dead_mt



gen cod_avg_weight=2204*(cod_kept_mt+cod_released_mt)/cod_tot_cat

gen haddock_relptrip=haddock_num_released/total_trips
gen haddock_landptrip=haddock_num_kept/total_trips

gen cod_relptrip=cod_num_released/total_trips
gen cod_landptrip=cod_num_kept/total_trips



label var haddock_relptrip "Haddock released/ trip"
label var haddock_landptrip "Haddock landed/ trip"
label var cod_landptrip "Cod landed/trip"
label var cod_relptrip "Cod released/trip"

label var total_trips  "Trips"
label var cod_mort_mt  "Cod Mortality (mt)"

label var hadd_mort_mt "Haddock Mortality (mt)"
label var cod_kept_mt "Cod Kept (mt)"
label var cod_released_mt "Cod Released (mt)"
label var hadd_kept_mt "Haddock Kept (mt)"
label var hadd_released_mt "Haddock Released(mt)"
label var cod_num_kept "Cod kept (#)"

label var cod_num_released "Cod released (#)"
label var haddock_num_kept "Haddock kept (#)"
label var haddock_num_released "Haddock relased (#)"
label var WTP "Willingness-to-Pay"

tempfile raw_results
save `raw_results'


/* scenario names */

sort scenario_num

gen cod_limit=1 
gen hadd_limit=20 

replace  hadd_limit=15 if inlist(scenario_num,3,4,5,18,19,20,17,217,218)
replace  hadd_limit=12 if inlist(scenario_num,6,7,8)
replace  hadd_limit=10 if inlist(scenario_num,9,10,11,101,111)
gen str10 hadd_size=`"17""'


replace  hadd_size=`"18""' if inlist(scenario_num,18,218)
replace  hadd_size=`"19""' if inlist(scenario_num,19)
replace  hadd_size=`"20""' if inlist(scenario_num,20)
gen cod_size=`"22""' 

gen str20 mode="All"
/*cod open seasons */

gen str60  cod_open_season="Sept 1 -Oct 7, April 1-14" if inlist(scenario_num,0,5,8,11)
replace  cod_open_season="Sept 1 -Sept 30" if inlist(scenario_num,1,3,6,9)
replace  cod_open_season="Sept 1 -Oct 31, April 1-April 30" if inlist(scenario_num,2,4,7,10)
replace  cod_open_season="Sept 1 -Oct 31" if inlist(scenario_num,101,17,18,19,20,217,218)
replace  cod_open_season="Sept 1 -Oct 31, April 1-15" if inlist(scenario_num,111)

/*haddock open seasons */

gen str60 had_open_season=" May-Feb28, Apr 1-Apr30" if inlist(scenario_num,0,1,2,3,4,5,6,7,8,9,10,11,101,111,18,19,20,17)
replace had_open_season=" May-Feb28" if inlist(scenario_num,217,218)

save `raw_results', replace






/****************************/
/****************************/
/* Contract to annual  */
/****************************/
/****************************/
collapse (sum) total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_mort_mt hadd_mort_mt cod_kept_mt cod_released_mt hadd_kept_mt hadd_released_mt cod_avg_weight haddock_relptrip haddock_landptrip cod_landptrip cod_relptrip  hadd_released_dead_mt cod_released_dead_mt WTP (first) cod_limit cod_size hadd_limit hadd_size mode cod_open_season had_open_season, by(scenario source scenario_num replicate )

/* Flag replicates where mortality less than subacl*/

gen cod_ok=0
replace cod_ok=1 if cod_mort_mt<=$cod_recACL2023
gen adj_cod_ok=0

gen adj_cod_mort_mt=cod_mort_mt + $cod_calibration_adj
replace adj_cod_ok=1 if adj_cod_mort_mt<=$cod_recACL2023




gen hadd_ok=0 
replace hadd_ok=1 if hadd_mort_mt<=$hadd_recACL2023

gen adj_hadd_ok=0
gen adj_hadd_mort_mt=hadd_mort_mt+ $hadd_calibration_adj
replace adj_hadd_ok=1 if adj_hadd_mort_mt<=$hadd_recACL2023

format adj_hadd_mort_mt adj_cod_mort_mt %6.1gc

/*Adjust cod mortality and haddock mortality components. 
I've added amount of motality to the cod and haddock components. I need to allocate that to 
*/

gen adj_cod_kept_mt=cod_kept_mt + cod_kept_mt/(cod_kept_mt+cod_released_dead_mt)*$cod_calibration_adj
gen adj_cod_released_dead_mt=cod_released_dead_mt+ cod_released_dead_mt/(cod_kept_mt+cod_released_dead_mt)*$cod_calibration_adj

gen adj_hadd_kept_mt=hadd_kept_mt + hadd_kept_mt/(hadd_kept_mt+hadd_released_dead_mt)*$hadd_calibration_adj
gen adj_hadd_released_dead_mt=hadd_released_dead_mt+ hadd_released_dead_mt/(hadd_kept_mt+hadd_released_dead_mt)*$hadd_calibration_adj



label var haddock_relptrip "Haddock released/ trip"
label var haddock_landptrip "Haddock landed/ trip"
label var cod_landptrip "Cod landed/trip"
label var cod_relptrip "Cod released/trip"

label var total_trips  "Trips"
label var cod_mort_mt  "Cod Mortality (mt)"

label var hadd_mort_mt "Haddock Mortality (mt)"
label var cod_kept_mt "Cod Kept (mt)"
label var cod_released_mt "Cod Released (mt)"
label var hadd_kept_mt "Haddock Kept (mt)"
label var hadd_released_mt "Haddock Released(mt)"
label var cod_ok "% Under Cod subACL"
label var hadd_ok "% Under Haddock subACL"


label var adj_cod_mort_mt  "Adjusted Cod Mortality (mt)"
label var adj_hadd_mort_mt "Adjusted Haddock Mortality (mt)"


label var adj_cod_ok "Adjusted % Under Cod subACL"
label var adj_hadd_ok "Adjusted % Under Haddock subACL"

label var source "Where is this data"

label var cod_num_kept "Cod kept (#)"

label var cod_num_released "Cod released (#)"
label var haddock_num_kept "Haddock kept (#)"
label var haddock_num_released "Haddock relased (#)"




label var adj_cod_kept_mt "Adjusted Cod Kept (mt)"
label var adj_cod_released_dead_mt  "Adjusted Cod Released Dead (mt)"

label var adj_hadd_kept_mt "Adjusted Haddock Kept (mt)"
label var adj_hadd_released_dead_mt "Adjusted Haddock Released Dead (mt)"


tempfile annual_results
save `annual_results', replace
<</dd_do>>



<<dd_do:quietly>>
/****************************/
/****************************/
/* Contract to medians  */
/****************************/
/****************************/
use  `annual_results', replace


collapse (p50) total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_mort_mt hadd_mort_mt cod_kept_mt cod_released_mt hadd_kept_mt hadd_released_mt cod_avg_weight haddock_relptrip haddock_landptrip cod_landptrip cod_relptrip  adj_cod_mort_mt adj_hadd_mort_mt hadd_released_dead_mt cod_released_dead_mt  adj_cod_kept_mt adj_cod_released_dead_mt adj_hadd_kept_mt adj_hadd_released_dead_mt WTP (sum) cod_ok hadd_ok adj_cod_ok adj_hadd_ok (count) N=replicate, by(scenario source scenario_num cod_limit cod_size hadd_limit hadd_size mode cod_open_season had_open_season)
replace cod_ok=cod_ok/N*100
replace hadd_ok=hadd_ok/N*100
replace adj_cod_ok=adj_cod_ok/N*100
replace adj_hadd_ok=adj_hadd_ok/N*100



label var haddock_relptrip "Haddock released/ trip"
label var haddock_landptrip "Haddock landed/ trip"
label var cod_landptrip "Cod landed/trip"
label var cod_relptrip "Cod released/trip"

label var total_trips  "Trips"
label var cod_mort_mt  "Cod Mortality (mt)"

label var hadd_mort_mt "Haddock Mortality (mt)"
label var cod_kept_mt "Cod Kept (mt)"
label var cod_released_mt "Cod Released (mt)"
label var hadd_kept_mt "Haddock Kept (mt)"
label var hadd_released_mt "Haddock Released(mt)"
label var cod_released_dead_mt "Cod Released Dead (mt)"
label var hadd_released_dead_mt "Haddock Released Dead(mt)"


label var cod_ok "% Under Cod subACL"

label var hadd_ok "% Under Haddock subACL"
label var source "Where is this data"

label var cod_num_kept "Cod kept (#)"

label var cod_num_released "Cod released (#)"
label var haddock_num_kept "Haddock kept (#)"
label var haddock_num_released "Haddock relased (#)"


label var adj_cod_mort_mt  "Adjusted Cod Mortality (mt)"
label var adj_hadd_mort_mt "Adjusted Haddock Mortality (mt)"



gen hadd_catch=haddock_num_kept+haddock_num_released

gen cod_catch=cod_num_kept+cod_num_released

tempfile summed_results


local graphlist 0,1,2,3,4,5,6,7,8,9,10,11,101,111,18,19,20,17 ,217,218
keep if inlist(scenario_num, `graphlist')
replace WTP=WTP/1000000

label var WTP "WTP millions"
/*reorder */

save `summed_results', replace

<</dd_do>>

 

## Summary Outputs

~~~~ 
<<dd_do:nocommands>>

use `summed_results', clear
format total_trips cod_mort_mt hadd_mort_mt hadd_catch cod_catch total_trips    %10.0fc 
format adj_hadd_kept_mt adj_hadd_released_dead_mt adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt %6.1fc
format WTP %6.1gc

qui gen str1 option=""
list option scenario_num mode cod_limit cod_size cod_open_season adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt  hadd_limit hadd_size  had_open_season adj_hadd_mort_mt adj_hadd_kept_mt adj_hadd_released_dead_mt total_trips adj_cod_ok adj_hadd_ok WTP if inlist(scenario_num,`graphlist'), abbreviate(16) table linesize(150) noobs divider separator(0)

list option scenario_num mode cod_limit cod_size cod_open_season hadd_limit hadd_size  had_open_season adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt  hadd_limit hadd_size  had_open_season  total_trips adj_cod_ok WTP  if inlist(scenario_num,`graphlist'), abbreviate(16) table linesize(150) noobs divider separator(0)


list option scenario_num mode cod_limit cod_size cod_open_season  hadd_limit hadd_size  had_open_season adj_hadd_mort_mt adj_hadd_kept_mt adj_hadd_released_dead_mt total_trips adj_cod_ok adj_hadd_ok WTP if inlist(scenario_num,`graphlist'), abbreviate(16) table linesize(150) noobs divider separator(0)

export excel option scenario_num mode cod_limit cod_size cod_open_season adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt  hadd_limit hadd_size  had_open_season adj_hadd_mort_mt adj_hadd_kept_mt adj_hadd_released_dead_mt total_trips adj_cod_ok adj_hadd_ok WTP using "${project_dir}/status_quo_summaries.xlsx" if inlist(scenario_num,`graphlist'), firstrow(variables) keepcellfmt replace

<</dd_do>>
~~~~

Narrative:

* Scenario 0 is the status quo. Mortality is slightly lower than 2022. This typically is due to changes in the age structure, typically an increase in younger and smaller fish 
* Scenarios 9 and 11 have a very high probability of the catch of both stocks being under their respective sub-ACLs.
* Scenario 101 has the same possession limits and size limits as Scenario 11.  October is opened to cod but April is closed to cod.
* Scenario 102 is similar to 101, however April is partially open to cod.  


