<<dd_version: 2>>


<!---A small document to read in and display some model results

THIS IS A PLACEHOLDER ---> 

<!---Execute this with:



if strmatch("$user","minyangWin"){
	global project_dir  "C:/Users/Min-Yang.Lee/Documents/BLAST/cod_haddock_fy2024" 
	global MRIP_root  "V:/READ-SSB-Lee-MRIP-BLAST/"
}



if strmatch("$user","minyangNix"){
	global project_dir "${myroot}/BLAST/READ-SSB-Lee-BLAST/cod_haddock_fy2024"
	global MRIP_root "${myroot}/BLAST/READ-SSB-Lee-MRIP-BLAST/" 
}

global mrip_vintage "2023_12_18"

/* setup directories */
global code_dir "${project_dir}/code"
global source_data "${project_dir}/source_data"
global working_data "${project_dir}/working_data"
global output_dir "${project_dir}/output"
global MRIP_dir  "${MRIP_root}/data_folder/main/MRIP_${mrip_vintage}"
global MRIP_raw  "${MRIP_root}/data_folder/raw"

dyndoc "${code_dir}/postsim/status_quo_summaries.txt", saving(${project_dir}/status_quo_summaries.html) replace

---> 
~~~~
<<dd_do:quietly>>
/****************************/
/****************************/
/* Haddock and Cod rec ACLs in mt*/
/****************************/
/****************************/
global haddock_mort2022=666


global cod_recACL2022=192
global hadd_recACL2022=3634

global cod_recACL2023=192
global hadd_recACL2023=610


global mrip_vintage "2023_12_18"

global hadd_recACL2024=759
global cod_recACL2024=192

global cod_calibration_adj= 21.2
global hadd_calibration_adj= 46.8


global adj_cod_recACL2024=$cod_recACL2024-$cod_calibration_adj
global adj_hadd_recACL2024=$hadd_recACL2024-$hadd_calibration_adj


<</dd_do>>
~~~~

# Inputs and Background

Fishing Year 2023 Regulations
==============================================================
The recreational sub-ACL for GoM cod is <<dd_display: %8.0fc $cod_recACL2023>> mt for FY 2023. The recreational sub-ACL for GOM haddock is <<dd_display: %8.0fc $hadd_recACL2023>> mt.  Regulations were implemented in August of 2023, so part of the year had a haddock limit of 20 fish at 17".



Haddock:

*    For-Hire sector: 15 fish with an 18" minimum size.
*    Private Anglers: 10 fish with a 17" minimum size.
*    Open May1-Feb 28 and April 1- April 30. Closed for the month of March.

Cod: 

*   1 fish, 22"
*   Open Sept 1-October 31 for all anglers.





Input data
==============================================================
1. MRIP data extracted and procesed on <<dd_display: "$mrip_vintage">>. 
1.  Monthly trips by mode (private boat, head, charter, shore)
1.  **Monthly** catch-per-trip for cod and haddock
1.  **Monthly** catch at length for cod and haddock
1.  2024 mean numbers at age for cod and haddock from the AGEPRO projections. See next section.
1.  Bottomtrawl survey data from 2021-2023 to form the age-length keys.
1.  Updated economic model parameters from Carr-Harris and Steinback (forthcoming in JOCE).



Changes from Previous Models
==============================================================
1. Length distributions of cod B2s **do not** use the MRIP survey weights. This is consistent with the stock assessment. 
1. Numbers-at-Age for cod and haddock use actual removals for cod (2021-2022) and haddock (2022) to inform AGEPRO model runs. This should give us a better estimate of 2024 NAA.
1. Calibration is done separately for the for-hire and private fleet. 


Numbers-at-Age for cod and haddock
==============================================================
The last stock assessment for cod was in 2021. The first year from this projection was 2020.  The off-the shelf "harvest scenario" specifies:

*    2020: 409.2 mt removals
*    2021: 523 mt removals
*    2022 and on: F=0.173

I've computed total removals=commercial + recreational landings + dead discards from MRIP data.  The removals that are passed to AGEPRO are:

*    2021: 348.7mt  = 215.7mt commercial removals + 33mt rec landings +  100 mt rec dead discards
*    2022: 548.3mt = 375.6mt commercial removals + 57.7mt rec landings +  115 mt rec dead discards
 
The last stock assessment for haddock was in 2022. The first year from this projection was 2022.  The off-the shelf "harvest scenario" specifies:

*    2022: 3912 mt of removals
*    2023 and on: F=0.338


I've computed recreational landings, discards, and dead discards from MRIP data.  I've extracted commercial landings from CAMS. The haddock removals that are passed to AGEPRO are:

*    2022: 3,365.8 = 2625.5mt commercial removals + 663.2mt rec landings +  77.1 mt rec dead discards

This gives us a slightly more accurate projection of 2024 Numbers-at-Age on Jan 1.




Calibration for the for-hire and private fleet
===============================================================

The 2022 and 2023 haddock regulations vary by mode.   Instead of calibrating to the total number of trips, we calibrate to each fleet: private (boat + shore) and for-hire (private + shore).  

1. I'm using the number of choice occasions to calibrate the model so the simulated number of trips matches the actual for each fleet
1. I'm using the "always on" behavior -- if an angler hits their bag limit for a species, they continue to fish but will discard any additional fish of that species, regardless of size.  For months when cod is closed, we set the possession limit to 5  and allow a small fraction of trips to retain fish.  This allows a very small number of trips to take a few cod. 
1. I use the regulations in place in the corresponding month. For May to October, this is the 2023 regulations. There are not trips from November to February.  For March and April this is actually the **2022 regulations**.
1. Since April of 2023 was open for approximately half of the month for cod, I calibrated by running 1 simulation where April is fully open for cod, one where April is fully closed for cod, and then I averaging the two. 
1. The costs of a trip vary by trip type (shore, private, head, charter).  I use the number of trips by mode, averaged over 2022-2023 to figure this out. Catch per trip and length of caught fish does not vary by trip type.  This is probably okay for everything except shore. There are *very* few shore trips.


# Simulations 

<!---
1. I'm using the number of choice occasions to calibrate the model so the simulated number of trips matches the actual.  
1. I'm using the "always on" behavior -- if an angler hits their bag limit for a species, they continue to fish but will discard any additional fish of that species, regardless of size.  For months when cod is closed, we set the possession limit to 5 and the minimum size to 100" -- this allows a very small number of trips to take a few cod. For months when haddock is closed, we set the possession limit to 15 and the minimum size to 100". This does the same thing. 
1.  I'm ignoring the Oct1 to Oct 7 cod opening for the for-hire fleet. But I'm also ignoring the Sept 1 to Sept 7 cod closure for the for hire fleet. So, we should look at Sept and October together.
SQ averages scenario numbers 1 and 3.   Scenario 1 is "April open for haddock, open for cod" and Scenario 3 is "April open for haddock, closed for cod."
Whole month of April for Cod and closed for haddock (scenario 4).
Whole month of April for both Cod and Haddock scenario 1.
--->


##  Scenario Key and Notes


| Scenario number| Short Description | Long Description| Status|
| :-----        |    :---------------   |          :--------------------- |   :-----|
| 0| Status Quo 2024       | The 2023 Regulations carry over to 2024.   | Run|
| 101| 18"/15 for haddock | These are the For Hire regs applied to the entire fleet   | Run|
| 102| 17"/10 for haddock      | These are the Private regs applied to the entire fleet| Run|
| 103| 18"/15 for haddock, 23" cod | FH plus an increase in cod size   | Run|
| 104| 17"/10 for haddock, 23" cod     | PR plus an increase in cod size| Run|
| 105| 18"/15 for haddock, 24" cod | FH plus an increase in cod size   | Run|
| 106| 17"/10 for haddock, 24" cod     | PR plus an increase in cod size| Run|



# Annual Results 

Here are some results.  In this section, I first compute the annual totals for every replicate. Then I report out on the medians.


<<dd_do:quietly>>


/****************************/
/****************************/
/* FOLDER setups */
/****************************/
/****************************/

/*minyangWin is setup to connect to oracle yet */
if strmatch("$user","minyangWin"){
	global project_dir  "C:/Users/Min-Yang.Lee/Documents/BLAST/cod_haddock_fy2024" 
	global MRIP_root  "V:/READ-SSB-Lee-MRIP-BLAST/"
	global 	oracle_cxn  " $mysole_conn"
}

if strmatch("$user","minyangNix"){
	global project_dir "${myroot}/BLAST/READ-SSB-Lee-BLAST/cod_haddock_fy2024"
	global MRIP_root "${myroot}/BLAST/READ-SSB-Lee-MRIP-BLAST/" 
}
/* setup directories */
global code_dir "${project_dir}/code"
global source_data "${project_dir}/source_data"
global working_data "${project_dir}/working_data"
global output_dir "${project_dir}/output"
global MRIP_dir  "${MRIP_root}/data_folder/main/MRIP_${mrip_vintage}"
global MRIP_raw  "${MRIP_root}/data_folder/raw"



local my_common1 "atlanticcod"
local my_common2 "haddock"


global cm_to_inch=0.39370787
global cod_naa "${source_data}/cod agepro/historical_and_mean_projected_Cod_NAA.dta"
global cod_naa_start "${source_data}/cod agepro/NAA_GOM_COD_2021_UPDATE_BOTH.dta"

global codalkey "${working_data}/cod_al_key.dta"



global working_year 2024
global previous=$working_year-1


global output_dir "${project_dir}/output"

/****************************/
/****************************/
/* Files to read in  */
/****************************/
/****************************/




/* Read in all the Econ model runs that match `stub' */
local estub "economic_data_2024_SQ_"

local estub2 "economic_data_2024_set1"
local estub3 "economic_data_2024_set2"
local estub4 "economic_data_2024_set3"

/*local estub3 "economic_data_2024_RAP_"
local estub4 "economic_data_2024_CMTE_" */

local efilelist1: dir "${output_dir}" files "`estub'*.dta"

local efilelist2: dir "${output_dir}" files "`estub2'*.dta"
local efilelist3: dir "${output_dir}" files "`estub3'*.dta"

local efilelist4: dir "${output_dir}" files "`estub4'*.dta"


*local ecombinedfiles `" `efilelist1' `efilelist2'  `efilelist3' `efilelist4' "'


local ecombinedfiles `" `efilelist1' `efilelist2'  `efilelist3' `efilelist4'"'


/****************************/
/****************************/
/* Read in results and parse the source  */
/****************************/
/****************************/
clear
gen str40 source=""
foreach file of local ecombinedfiles{
capture append using ${output_dir}/`file'
replace source="`file'"  if source==""
}
keep scenario scenario_num month replicate WTP


tempfile econ_data
save `econ_data', replace









/* Read in all the model runs that match `stub' */
local stub "recreational_catches_2024_SQ_"

local stub2 "recreational_catches_2024_set1"
local stub3 "recreational_catches_2024_set2"
local stub4 "recreational_catches_2024_set3"
/*
local stub3 "recreational_catches_2024_RAP_"
local stub4 "recreational_catches_2024_CMTE_" */

local filelist1: dir "${output_dir}" files "`stub'*.dta"

local filelist2: dir "${output_dir}" files "`stub2'*.dta"
local filelist3: dir "${output_dir}" files "`stub3'*.dta"
local filelist4: dir "${output_dir}" files "`stub4'*.dta" 

*local combinedfiles `" `filelist1' `filelist2' `filelist3' `filelist4' `filelist5' "'

local combinedfiles `" `filelist1' `filelist2' `filelist3' `filelist4' "'


/****************************/
/****************************/
/* Read in results and parse the source  */
/****************************/
/****************************/
clear
gen str40 source=""
foreach file of local combinedfiles{
capture append using ${output_dir}/`file'
replace source="`file'"  if source==""
}

merge 1:1 scenario scenario_num month replicate using `econ_data'

split source, parse("/") gen(ss)
scalar rnvars=r(nvars)
local all=r(varlist)
local m="ss"+scalar(rnvars)
local dropper : list all - m


/****************************/
/****************************/
/* Tidy ups
Drop the rows corresponding to the previous fishing year. 
Generate the fishing year variable
construct total catch (numbers) and mortality (mt)
 */
/****************************/
/****************************/



drop if month<=4
gen fishing_year=$working_year
gen year=fishing_year
gen month_of_fy=month-4

replace year=year+1 if month>=13
replace month=month-12 if month>=13



/* need to sum up the FH and PR modes, by "scenario" */


replace scenario_num=0 if inlist(scenario_num, 3, 4)
replace scenario_num=101 if inlist(scenario_num, 5, 6)
replace scenario_num=102 if inlist(scenario_num, 7, 8)

replace scenario_num=103 if inlist(scenario_num, 9, 10)
replace scenario_num=104 if inlist(scenario_num, 11,12)

replace scenario_num=105 if inlist(scenario_num, 13, 14)
replace scenario_num=106 if inlist(scenario_num, 15,16)



/* aggregate the fleet outcomes to the scenario level*/
collapse (sum) choice_occasions total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_kept_mt cod_released_mt cod_released_dead_mt hadd_kept_mt hadd_released_mt hadd_released_dead_mt crep hrep codbag_comply cod_sublegal_keep cod_release_mort hadd_release_mort WTP ,by(scenario_num fishing_year year month replicate month_of_fy source)

tempfile s_as_run
save `s_as_run', replace emptyok










use `s_as_run', clear


/* Leaving the machinery to weight different scenarios, if necessary*/

gen freq=1

/*compute averages if necessary*/
collapse (mean) choice_occasions total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_kept_mt cod_released_mt cod_released_dead_mt hadd_kept_mt hadd_released_mt hadd_released_dead_mt crep hrep codbag_comply cod_sublegal_keep cod_release_mort hadd_release_mort WTP [fweight=freq] ,by(scenario_num fishing_year year month replicate month_of_fy source)


tempfile smodified
save `smodified', replace emptyok




gen cod_tot_cat=cod_num_kept+cod_num_released
gen hadd_tot_cat=haddock_num_kept+haddock_num_released

sort month
order cod_tot_cat hadd_tot_cat, after(month)
format *num* %09.1gc
format *mt %06.1fc
format *tot_cat %09.1gc

gen cod_mort_mt=cod_kept_mt+cod_released_dead_mt
gen hadd_mort_mt=hadd_kept_mt+hadd_released_dead_mt



gen cod_avg_weight=2204*(cod_kept_mt+cod_released_mt)/cod_tot_cat

gen haddock_relptrip=haddock_num_released/total_trips
gen haddock_landptrip=haddock_num_kept/total_trips

gen cod_relptrip=cod_num_released/total_trips
gen cod_landptrip=cod_num_kept/total_trips



label var haddock_relptrip "Haddock released/ trip"
label var haddock_landptrip "Haddock landed/ trip"
label var cod_landptrip "Cod landed/trip"
label var cod_relptrip "Cod released/trip"

label var total_trips  "Trips"
label var cod_mort_mt  "Cod Mortality (mt)"

label var hadd_mort_mt "Haddock Mortality (mt)"
label var cod_kept_mt "Cod Kept (mt)"
label var cod_released_mt "Cod Released (mt)"
label var hadd_kept_mt "Haddock Kept (mt)"
label var hadd_released_mt "Haddock Released(mt)"
label var cod_num_kept "Cod kept (#)"

label var cod_num_released "Cod released (#)"
label var haddock_num_kept "Haddock kept (#)"
label var haddock_num_released "Haddock relased (#)"
label var WTP "Willingness-to-Pay"

tempfile raw_results
save `raw_results'


/* scenario names */

sort scenario_num
gen cod_limit=1 
gen str60 hadd_limit=`"FH 15, PR 10"' if inlist(scenario_num,0) 
gen str60 hadd_size=`"FH 18", PR 17""' if inlist(scenario_num,0)
gen cod_size=`"22""' 
replace cod_size=`"23""' if inlist(scenario_num,103,104)
replace cod_size=`"24""' if inlist(scenario_num,105,106)
gen str20 mode="Combined"
/*cod open seasons */
gen str60  cod_open_season="Sept 1 -Oct 31" if inlist(scenario_num,0,101,102,103,104,105,106)

/*haddock open seasons */
gen str60 had_open_season=" May-Feb28, Apr 1-Apr30" if inlist(scenario_num,0,101,102,103,104,105,106)



replace hadd_limit=`"15"' if inlist(scenario_num,101,103,105)
replace hadd_limit=`"10"' if inlist(scenario_num,102,104,106)
replace hadd_size=`"18"' if inlist(scenario_num,101,103,105)
replace hadd_size=`"17"' if inlist(scenario_num,102,104,106)
save `raw_results', replace






/****************************/
/****************************/
/* Contract to annual  */
/****************************/
/****************************/
collapse (sum) total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_mort_mt hadd_mort_mt cod_kept_mt cod_released_mt hadd_kept_mt hadd_released_mt cod_avg_weight haddock_relptrip haddock_landptrip cod_landptrip cod_relptrip  hadd_released_dead_mt cod_released_dead_mt WTP (first) cod_limit cod_size hadd_limit hadd_size mode cod_open_season had_open_season, by(scenario source scenario_num replicate )

/* Flag replicates where mortality less than subacl*/

gen cod_ok=0
replace cod_ok=1 if cod_mort_mt<=$cod_recACL2024
gen adj_cod_ok=0

gen adj_cod_mort_mt=cod_mort_mt + $cod_calibration_adj
replace adj_cod_ok=1 if adj_cod_mort_mt<=$cod_recACL2024




gen hadd_ok=0 
replace hadd_ok=1 if hadd_mort_mt<=$hadd_recACL2024

gen adj_hadd_ok=0
gen adj_hadd_mort_mt=hadd_mort_mt+ $hadd_calibration_adj
replace adj_hadd_ok=1 if adj_hadd_mort_mt<=$hadd_recACL2024

format adj_hadd_mort_mt adj_cod_mort_mt %6.1gc

/*Adjust cod mortality and haddock mortality components. 
I've added amount of motality to the cod and haddock components. I need to allocate that to 
*/

gen adj_cod_kept_mt=cod_kept_mt + cod_kept_mt/(cod_kept_mt+cod_released_dead_mt)*$cod_calibration_adj
gen adj_cod_released_dead_mt=cod_released_dead_mt+ cod_released_dead_mt/(cod_kept_mt+cod_released_dead_mt)*$cod_calibration_adj

gen adj_hadd_kept_mt=hadd_kept_mt + hadd_kept_mt/(hadd_kept_mt+hadd_released_dead_mt)*$hadd_calibration_adj
gen adj_hadd_released_dead_mt=hadd_released_dead_mt+ hadd_released_dead_mt/(hadd_kept_mt+hadd_released_dead_mt)*$hadd_calibration_adj



label var haddock_relptrip "Haddock released/ trip"
label var haddock_landptrip "Haddock landed/ trip"
label var cod_landptrip "Cod landed/trip"
label var cod_relptrip "Cod released/trip"

label var total_trips  "Trips"
label var cod_mort_mt  "Cod Mortality (mt)"

label var hadd_mort_mt "Haddock Mortality (mt)"
label var cod_kept_mt "Cod Kept (mt)"
label var cod_released_mt "Cod Released (mt)"
label var hadd_kept_mt "Haddock Kept (mt)"
label var hadd_released_mt "Haddock Released(mt)"
label var cod_ok "% Under Cod subACL"
label var hadd_ok "% Under Haddock subACL"


label var adj_cod_mort_mt  "Adjusted Cod Mortality (mt)"
label var adj_hadd_mort_mt "Adjusted Haddock Mortality (mt)"


label var adj_cod_ok "Adjusted % Under Cod subACL"
label var adj_hadd_ok "Adjusted % Under Haddock subACL"

label var source "Where is this data"

label var cod_num_kept "Cod kept (#)"

label var cod_num_released "Cod released (#)"
label var haddock_num_kept "Haddock kept (#)"
label var haddock_num_released "Haddock relased (#)"




label var adj_cod_kept_mt "Adjusted Cod Kept (mt)"
label var adj_cod_released_dead_mt  "Adjusted Cod Released Dead (mt)"

label var adj_hadd_kept_mt "Adjusted Haddock Kept (mt)"
label var adj_hadd_released_dead_mt "Adjusted Haddock Released Dead (mt)"


tempfile annual_results
save `annual_results', replace
<</dd_do>>



<<dd_do:quietly>>
/****************************/
/****************************/
/* Contract to medians  */
/****************************/
/****************************/
use  `annual_results', replace


collapse (p50) total_trips cod_num_kept cod_num_released haddock_num_kept haddock_num_released cod_mort_mt hadd_mort_mt cod_kept_mt cod_released_mt hadd_kept_mt hadd_released_mt cod_avg_weight haddock_relptrip haddock_landptrip cod_landptrip cod_relptrip  adj_cod_mort_mt adj_hadd_mort_mt hadd_released_dead_mt cod_released_dead_mt  adj_cod_kept_mt adj_cod_released_dead_mt adj_hadd_kept_mt adj_hadd_released_dead_mt WTP (sum) cod_ok hadd_ok adj_cod_ok adj_hadd_ok (count) N=replicate, by(scenario source scenario_num cod_limit cod_size hadd_limit hadd_size mode cod_open_season had_open_season)
replace cod_ok=cod_ok/N*100
replace hadd_ok=hadd_ok/N*100
replace adj_cod_ok=adj_cod_ok/N*100
replace adj_hadd_ok=adj_hadd_ok/N*100



label var haddock_relptrip "Haddock released/ trip"
label var haddock_landptrip "Haddock landed/ trip"
label var cod_landptrip "Cod landed/trip"
label var cod_relptrip "Cod released/trip"

label var total_trips  "Trips"
label var cod_mort_mt  "Cod Mortality (mt)"

label var hadd_mort_mt "Haddock Mortality (mt)"
label var cod_kept_mt "Cod Kept (mt)"
label var cod_released_mt "Cod Released (mt)"
label var hadd_kept_mt "Haddock Kept (mt)"
label var hadd_released_mt "Haddock Released(mt)"
label var cod_released_dead_mt "Cod Released Dead (mt)"
label var hadd_released_dead_mt "Haddock Released Dead(mt)"


label var cod_ok "% Under Cod subACL"

label var hadd_ok "% Under Haddock subACL"
label var source "Where is this data"

label var cod_num_kept "Cod kept (#)"

label var cod_num_released "Cod released (#)"
label var haddock_num_kept "Haddock kept (#)"
label var haddock_num_released "Haddock relased (#)"


label var adj_cod_mort_mt  "Adjusted Cod Mortality (mt)"
label var adj_hadd_mort_mt "Adjusted Haddock Mortality (mt)"



gen hadd_catch=haddock_num_kept+haddock_num_released

gen cod_catch=cod_num_kept+cod_num_released

tempfile summed_results


local graphlist 0,101,102,103,104,105,106
keep if inlist(scenario_num, `graphlist')
replace WTP=WTP/1000000

label var WTP "WTP millions"
/*reorder */

save `summed_results', replace

<</dd_do>>

 

## Summary Outputs

~~~~ 
<<dd_do:nocommands>>

use `summed_results', clear
format total_trips cod_mort_mt hadd_mort_mt hadd_catch cod_catch total_trips    %10.0fc 
format adj_hadd_kept_mt adj_hadd_released_dead_mt adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt %6.1fc
format WTP %6.1gc
qui gen str1 option=""

/*
list option scenario_num cod_limit cod_size cod_open_season adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt  hadd_limit hadd_size  had_open_season adj_hadd_mort_mt adj_hadd_kept_mt adj_hadd_released_dead_mt total_trips adj_cod_ok adj_hadd_ok WTP if inlist(scenario_num,`graphlist'), abbreviate(16) table linesize(160) noobs divider separator(0)

list option scenario_num cod_limit cod_size cod_open_season hadd_limit hadd_size  had_open_season adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt  hadd_limit hadd_size  had_open_season  total_trips adj_cod_ok WTP  if inlist(scenario_num,`graphlist'), abbreviate(16) table linesize(160) noobs divider separator(0)


list option scenario_num cod_limit cod_size cod_open_season  hadd_limit hadd_size  had_open_season adj_hadd_mort_mt adj_hadd_kept_mt adj_hadd_released_dead_mt total_trips adj_cod_ok adj_hadd_ok WTP if inlist(scenario_num,`graphlist'), abbreviate(16) table linesize(160) noobs divider separator(0)
*/

list option scenario_num total_trips adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt adj_cod_ok adj_hadd_mort_mt adj_hadd_kept_mt adj_hadd_released_dead_mt adj_hadd_ok WTP if inlist(scenario_num,`graphlist'), abbreviate(16) table linesize(160) noobs divider separator(0)


export excel option scenario_num mode cod_limit cod_size cod_open_season adj_cod_mort_mt adj_cod_kept_mt adj_cod_released_dead_mt  hadd_limit hadd_size  had_open_season adj_hadd_mort_mt adj_hadd_kept_mt adj_hadd_released_dead_mt total_trips adj_cod_ok adj_hadd_ok WTP using "${project_dir}/status_quo_summaries.xlsx" if inlist(scenario_num,`graphlist'), firstrow(variables) keepcellfmt replace

<</dd_do>>
~~~~

<!---
Narrative:

* Scenario 0 is the status quo. 
* Scenario 11 :  15 Haddock at 18" 
* Scenario 12 : 10 Haddock at 17"
--->
